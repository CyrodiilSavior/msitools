AC_INIT([msitools], [0.01])
AC_PREREQ([2.64])

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR([libmsi/sql-parser.y])
AM_INIT_AUTOMAKE([foreign -Wno-portability])

AC_CANONICAL_HOST
AS_CASE([$host], [*-*-mingw*|*-*-cygwin*], [win32=yes], [win32=no])
AM_CONDITIONAL([WIN32], [test $win32 = yes])

AC_USE_SYSTEM_EXTENSIONS
AC_PROG_CC
AC_PROG_YACC

IT_PROG_INTLTOOL([0.35])

AM_PATH_GLIB_2_0([2.12.0])
PKG_CHECK_MODULES([GOBJECT], [gobject-2.0 gio-2.0 >= 2.14])
PKG_CHECK_MODULES([GSF], [libgsf-1])
PKG_CHECK_MODULES([UUID], [uuid], [uuid=yes], [uuid=no])
AS_IF([test "$uuid" = yes],
      [AC_DEFINE([HAVE_LIBUUID], 1,
                 [Define to 1 if libuuid is present on the system])],
      [AC_MSG_WARN([libuuid not found, msibuild will generate packages without a GUID])])

LT_INIT([win32-dll disable-fast-install])
GOBJECT_INTROSPECTION_CHECK([0.9.4])
AM_CONDITIONAL([GIR], [test "x$INTROSPECTION_MAKEFILE" != x])

AM_PROG_VALAC([0.16])
AC_PATH_PROG(VAPIGEN, vapigen, no)
AC_SUBST(VAPIGEN)
AM_CONDITIONAL([VAPI], [test "x$VAPIGEN" != xno])

AS_IF([test $win32 = no],
      [WIXL_UNIX="gio-unix-2.0"])

PKG_CHECK_MODULES([WIXL], [gio-2.0 >= 2.26.0
                           libgcab-1.0
                           uuid >= 1.41.3
                           libxml-2.0 >= 2.9
                           $WIXL_UNIX],
                  [wixl_ok=yes], [wixl_ok=no])

AC_ARG_ENABLE([wixl],
              [AS_HELP_STRING([--disable-wixl], [do not build wixl (default=yes)])],
              [wixl=$enableval], [wixl=yes])

AS_IF([test $wixl_ok = no && test $wixl != no],
      [AC_MSG_ERROR([Wixl dependencies not found])])

AM_CONDITIONAL([WIXL], [test "x$wixl" != xno])

GETTEXT_PACKAGE=wixl
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE], ["$GETTEXT_PACKAGE"], [Gettext Package])
AC_SUBST(GETTEXT_PACKAGE)
AM_GNU_GETTEXT_VERSION([1.11])
AM_GLIB_GNU_GETTEXT

m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

AM_MISSING_PROG([AUTOM4TE], [autom4te])
AC_CONFIG_TESTDIR([.])
AC_CONFIG_FILES([atlocal])
AC_CONFIG_FILES([tests/package.m4.tmp:tests/package.m4.in],
                [$srcdir/build-aux/move-if-change tests/package.m4.tmp $srcdir/tests/package.m4
                 cp $srcdir/tests/package.m4 tests/package.m4.tmp])

AC_CONFIG_FILES([
    Makefile
    data/Makefile
    po/Makefile.in
    libmsi/Makefile
    libmsi/libmsi-1.0.pc
    tests/Makefile
    tests/data/Makefile
])
AC_OUTPUT

AC_MSG_NOTICE([
        msitools $VERSION
        ================

        prefix:                   ${prefix}
        c compiler:               ${CC}
        uuid:                     ${uuid}
        wixl:                     ${wixl}
])
